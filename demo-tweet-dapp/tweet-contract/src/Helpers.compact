module Helpers{
    import CompactStandardLibrary;
    export {TVL, ReceiveFundAndAddToTVL, SendFundAndManageChange, SendFundImmediately};

    ledger TVL: QualifiedShieldedCoinInfo;

    export circuit ReceiveFundAndAddToTVL(disclosedCoin: ShieldedCoinInfo): [] {
        /** Recieves coin into the contract */
        receiveShielded(disclosedCoin);

        const coinToInsert = TVL.value > 0 ? mergeCoinImmediate(TVL, disclosedCoin) : disclosedCoin;

        TVL.writeCoin(coinToInsert, right<ZswapCoinPublicKey, ContractAddress>(
            kernel.self()
        ));
    }
    
    export circuit SendFundAndManageChange(amt: Uint<128>, receiver: Bytes<32>): [] {
        const sendResult = sendShielded(TVL, left<ZswapCoinPublicKey, ContractAddress>(ZswapCoinPublicKey{
            bytes: receiver
        }), amt);

        if(sendResult.change.is_some){
            TVL.writeCoin(sendResult.change.value, right<ZswapCoinPublicKey, ContractAddress>(
            kernel.self()
            ));
        }else{
            TVL.resetToDefault();
        }
    }

    export circuit SendFundImmediately(amt: Uint<128>, receiver: Bytes<32>, disclosedCoin: ShieldedCoinInfo): [] {
        /** Recieves coin into the contract */
        receiveShielded(disclosedCoin);
        
        sendImmediateShielded(disclosedCoin, left<ZswapCoinPublicKey, ContractAddress>(ZswapCoinPublicKey{
            bytes: receiver
        }), amt);
    }
}